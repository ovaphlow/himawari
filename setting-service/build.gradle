buildscript {
    ext.kotlin_version = "1.3.61"
    ext.coroutines_version = "1.3.3"
    ext.protobuf_version = "3.11.1"
    ext.grpc_version = "1.28.1" // CURRENT_GRPC_VERSION
    ext.grpc_kotlin_version = "0.1.3" // CURRENT_GRPC_KOTLIN_VERSION

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.11"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "com.google.protobuf"
apply plugin: "application"
apply plugin: "idea"

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation "io.grpc:grpc-kotlin-stub:$grpc_kotlin_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    implementation "com.google.protobuf:protobuf-java:$protobuf_version"
    implementation "com.google.protobuf:protobuf-java-util:$protobuf_version"
    implementation "io.grpc:grpc-netty-shaded:$grpc_version"
    implementation "io.grpc:grpc-protobuf:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"

    compileOnly "javax.annotation:javax.annotation-api:1.2"

    implementation "com.google.guava:guava:28.2-jre"

    implementation "commons-cli:commons-cli:1.4"
    implementation "ch.qos.logback:logback-classic:1.2.3"

    implementation "org.postgresql:postgresql:42.2.12"
    implementation "com.zaxxer:HikariCP:3.4.2"
    implementation 'commons-dbutils:commons-dbutils:1.7'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protobuf_version" }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpc_kotlin_version"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc { }
                grpckt { }
            }
        }
    }
}

startScripts.enabled = false

task settingService(type: CreateStartScripts) {
    mainClassName = 'ovaphlow.himawari.service.setting.Application'
    applicationName = 'ovaphlow.himawari.service.setting.202006-1'
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(settingService)
    fileMode = 0755
}
